.include "common.inc"

    # Configure trap vector
    LUI  x1, %hi(trap_entry)
    ADDI x1, x1, %lo(trap_entry)
    CSRRW x0, mtvec, x1

    # Reset timer and program compare
    ADDI x2, x0, 0
    CSRRW x0, mtime, x2
    ADDI x3, x0, TIMER_INTERVAL
    CSRRW x0, mtimecmp, x3

    # Enable MTIE + MEIE in mie
    ADDI x4, x0, 0x80
    ADDI x5, x0, 0x7FF
    ADDI x5, x5, 1
    ADD  x4, x4, x5
    CSRRW x0, mie, x4

    # Enable global MIE
    ADDI x6, x0, 0x8
    CSRRS x0, mstatus, x6

    ADDI x10, x0, 0       # timer interrupt counter
    ADDI x11, x0, 0       # external interrupt counter

    JAL  x0, main_loop

trap_entry:
    CSRRS x5, mcause, x0
    LUI   x6, MCAUSE_TIMER_HI
    ADDI  x6, x6, MCAUSE_TIMER_LO
    BEQ   x5, x6, handle_timer
    LUI   x6, MCAUSE_EXT_HI
    ADDI  x6, x6, MCAUSE_EXT_LO
    BEQ   x5, x6, handle_ext
    JAL   x0, handle_ecall

handle_timer:
    ADDI x10, x10, 1
    SW   x10, TIMER_RESULT_ADDR(x0)
    ADDI x13, x0, IRQ_TARGET_TIMERS
    BLT  x10, x13, timer_rearm
    JAL  x0, timer_disable
timer_rearm:
    CSRRS x7, mtimecmp, x0
    ADDI x7, x7, TIMER_INTERVAL
    CSRRW x0, mtimecmp, x7
    MRET
timer_disable:
    ADDI x14, x0, 0x80
    CSRRC x0, mie, x14
    MRET

handle_ext:
    ADDI x11, x11, 1
    SW   x11, EXT_RESULT_ADDR(x0)
    MRET

handle_ecall:
    SW   x10, TIMER_RESULT_ADDR(x0)
    SW   x11, EXT_RESULT_ADDR(x0)
    ADDI x12, x0, 0x1EE
    SW   x12, ECALL_RESULT_ADDR(x0)
    JAL  x0, program_end

main_loop:
    ADDI x12, x0, IRQ_TARGET_TIMERS
    BLT  x10, x12, loop_cont
    ADDI x13, x0, IRQ_TARGET_EXTERNAL
    BLT  x11, x13, loop_cont
    ECALL

loop_cont:
    JAL  x0, main_loop

program_end:
    JAL  x0, program_end
